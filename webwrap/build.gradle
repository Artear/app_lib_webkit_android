buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:$bintrayVersion"
        classpath "com.github.dcendents:android-maven-gradle-plugin:$dcendentsVersion"
        classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:$dokkaVersion"
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

ext {
    bintrayRepo = 'Android'
    bintrayName = 'Webwrap'

    publishedGroupId = 'com.artear.webwrap'
    libraryName = 'Webwrap'
    artifact = 'webwrap'

    libraryDescription = 'webwrap sample library'

    siteUrl = 'https://github.com/Artear/app_lib_webwrap_android'
    gitUrl = 'https://github.com/Artear/app_lib_webwrap_android.git'

    developerId = 'artear'
    developerName = 'Artear Moviles'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

static int getVersionCode(branch) {
    def versionCodeCommand = "git rev-list --count " + branch
    return versionCodeCommand.execute().text.trim().toInteger()
}

static def getBranchName() {
    return "git rev-parse --abbrev-ref HEAD".execute().text.trim()
}

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    def branch = getBranchName()
    def vCode = 0 + getVersionCode(branch)

    libraryVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${project.getName()}-${defaultConfig.versionName}-${variant.buildType.name}.aar"
        }
    }

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 28
        versionCode vCode
        versionName libraryVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //Test
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test:runner:$testRunnerVersion"
    androidTestImplementation "androidx.test.ext:junit:$extJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoCoreVersion"

    //Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    //AndroidX
    implementation "androidx.appcompat:appcompat:${appCompatVersion}"

    //Artear
    implementation "com.artear.multitracker:multitracker:$multiTrackerVersion"
    implementation "com.artear.tools:tools:$toolsVersion"
    implementation "com.artear.ui:view:$uiViewVersion"
    implementation "com.artear.domain:domain:$domainVersion"
    implementation "com.artear.injector:injector-api:$injectorVersion"
    kapt "com.artear.injector:injector-compiler:$injectorVersion"

    //Logger
    implementation "com.jakewharton.timber:timber:$timberVersion"

    //Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"

    implementation "com.squareup.moshi:moshi:$moshiVersion"
    implementation "com.squareup.okio:okio:$okio"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"
}

group = publishedGroupId
version = libraryVersion

apply from: 'publication.gradle'
apply from: 'bintray.gradle'
apply plugin: 'idea'

idea {
    module {
        sourceDirs += files('build/generated/source/kapt/main', 'build/generated/source/kaptKotlin/main')
        generatedSourceDirs += files('build/generated/source/kapt/main', 'build/generated/source/kaptKotlin/main')
    }
}